(define (entry tree) (car tree))
(define (left-branch tree) (cadr tree))
(define (right-branch tree) (caddr tree))
(define (make-tree entry left right)
  (list entry left right))

(define (list->tree elements)
  (car (partial-tree elements (length elements))))

(define (tree->list tree)
  (define (copy-to-list tree result-list)
    (if (null? tree)
        result-list
        (copy-to-list (left-branch tree)
                      (cons (entry tree)
                            (copy-to-list (right-branch tree)
                                          result-list)))))
  (copy-to-list tree '()))

(define (partial-tree elts n)
  (if (= n 0)
      (cons '() elts)
      (let ((left-size (quotient (- n 1) 2)))
        (let ((left-result (partial-tree elts left-size)))
          (let ((left-tree (car left-result))
                (non-left-elts (cdr left-result))
                (right-size (- n (+ left-size 1))))
            (let ((this-entry (car non-left-elts))
                  (right-result (partial-tree (cdr non-left-elts)
                                              right-size)))
              (let ((right-tree (car right-result))
                    (remaining-elts (cdr right-result)))
                (cons (make-tree this-entry left-tree right-tree)
                      remaining-elts))))))))

(define (intersection-set tree1 tree2)
  (define (intersection-set-ord-list set1 set2)
    (if (or (null? set1) (null? set2))
        '()
        (let ((x1 (car set1)) (x2 (car set2)))
          (cond ((= x1 x2)
                 (cons x1
                       (intersection-set-ord-list (cdr set1)
                                         (cdr set2))))
                ((< x1 x2)
                 (intersection-set-ord-list (cdr set1) set2))
                ((< x2 x1)
                 (intersection-set-ord-list set1 (cdr set2)))))))
  (list->tree (intersection-set-ord-list (tree->list tree1)
                                         (tree->list tree2))))

(define (union-set tree1 tree2)
  (define (union-set-ord-list set1 set2)
    (cond ((null? set1) set2)
          ((null? set2) set1)
          (else (let ((x1 (car set1))
                      (x2 (car set2)))
                  (cond ((= x1 x2)
                          (cons x1 (union-set-ord-list (cdr set1) (cdr set2))))
                        ((< x1 x2)
                          (cons x1
                                  (union-set-ord-list (cdr set1) set2)))
                        ((< x2 x1)
                          (cons x2
                                  (union-set-ord-list set1 (cdr set2)))))))))
  (list->tree (union-set-ord-list (tree->list tree1)
                                  (tree->list tree2))))

(define evens (list->tree '(0 2 4 6 8 10)))
(define odds (list->tree '(1 3 5 7 9)))
(define primes (list->tree '(2 3 5 7 11 13 17 19)))

(union-set odds evens)
;'(5
;  (2 (0 () (1 () ())) (3 () (4 () ())))
;  (8 (6 () (7 () ())) (9 () (10 () ()))))

(union-set odds odds)
;=> (5 (1 () (3 () ())) (7 () (9 () ())))

(intersection-set evens primes)
;=> (2 () ())

(intersection-set odds primes)
;=> (5 (3 () ()) (7 () ()))

(intersection-set odds evens)
;=> ()
